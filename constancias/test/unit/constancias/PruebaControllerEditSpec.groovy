package constancias

import javax.servlet.http.HttpServletRequest
import grails.test.GrailsMock
import grails.test.mixin.*
import spock.lang.*

@TestFor(PruebaController)
@Mock(Prueba)
class PruebaControllerEditSpec extends Specification {

    def setup() {

        PruebaMock.mock( 0 ).save( failOnError:true )
        views[ '/prueba/_form.gsp' ] = getTemplate()

    }

    def "test ok"() {

        when:
            def control = mockPruebaService()
            request.method = 'GET'
            def model = controller.edit( 1 )
            control.verify()
        then:
            response.text == 'OK'
            response.status == 200

    }

    def "test id null"() {

        when:
            def control = mockCrackingService()
            request.method = 'GET'
            controller.edit( null )
            control.verify()
        then:
            response.redirectedUrl == '/logout'
            response.status == 302

    }

    def "test not found"() {

        when:
            def control = mockPruebaService()
            def control2 = mockCrackingService()
            request.method = 'GET'
            controller.edit( 2 )
            control.verify()
            control2.verify()
        then:
            response.redirectedUrl == '/logout'
            response.status == 302

    }

    @Ignore( 'See http://jira.grails.org/browse/GRAILS-8426' )
    def "test request method invalid"() {

        when:
            request.method = 'POST'
            controller.edit( 2 )
        then:
            response.status == 405

    }

    private String getTemplate() {
        '<g:if test="${pruebaInstance}">OK</g:if><g:else>ERROR</g:else>'
    }

    private GrailsMock mockPruebaService() {

        def control = mockFor( PruebaService )
        control.demand.get( 1 ) { Long id ->
            Prueba.findById( id )
        }
        controller.pruebaService = control.createMock()
        control

    }

    private GrailsMock mockCrackingService() {

        def control = mockFor( CrackingService )
        control.demand.notify( 1 ) { HttpServletRequest request, Map params -> }
        controller.crackingService = control.createMock()
        control

    }

}